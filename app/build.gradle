apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://maven.google.com' }
    google()
    mavenCentral()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.playlistappkotlin"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "PlaylistApp-$versionName"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
                includeCompileClasspath false
            }
        }
        multiDexEnabled true
    }
    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
                proguardFile 'proguard-rules.pro'
            }
            applicationVariants.all { variant ->
                variant.outputs.all {
                    def date = new Date()
                    def formattedDate = date.format('yyyyMMddHHmmss')
                    outputFileName = "${variant.name.replace("-release", "-" + formattedDate)}-${variant.versionName}.apk"
                }
            }

            buildConfigField("String", "BASE_URL", "\"http://ws.audioscrobbler.com/2.0/\"")
            buildConfigField("String", "API_KEY", "\"56532a164dfe077a343683169bb3a201\"")
        }

        debug {
            minifyEnabled false
            buildConfigField("String", "BASE_URL", "\"http://ws.audioscrobbler.com/2.0/\"")
            buildConfigField("String", "API_KEY", "\"56532a164dfe077a343683169bb3a201\"")
        }
    }

    kapt {
        generateStubs = true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }

    afterEvaluate {
        tasks.matching {
            it.name.startsWith('dex')
        }.each { dx ->
            if (dx.additionalParameters == null) {
                dx.additionalParameters = []
            }
            dx.additionalParameters += "--set-max-idx-number=55000"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:$deps.supportLibraryVersion"
    }
}

greendao {
    schemaVersion 1
}

apply from: "dependencies.gradle"

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}
